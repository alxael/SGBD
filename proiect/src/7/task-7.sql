CREATE TYPE OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOC IS
    OBJECT (
        COD_LOC VARCHAR2(20),
        STATUS VARCHAR2(20),
        NUME_COMPLET VARCHAR2(255)
    )
/

CREATE TYPE OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOCURI IS
    TABLE OF OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOC;
/

CREATE OR REPLACE FUNCTION OCUPARE_LOCURI_LA_DIFUZARE (
    ID_DIFUZARE_PARAM DIFUZARI.ID_DIFUZARE%TYPE
) RETURN OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOCURI AS
    CURSOR REZERVARE_LOC(
        ID_LOC_PARAM LOCURI.ID_LOC%TYPE
    ) IS (
        SELECT
            *
        FROM
            REZERVARI R
        WHERE
            R.ID_DIFUZARE = ID_DIFUZARE_PARAM
            AND R.ID_LOC = ID_LOC_PARAM
    );
    DATE_REZERVARE    REZERVARI%ROWTYPE;
    NUME_COMPLET      VARCHAR2(255);
    REZERVARI_PER_LOC NUMBER(10);
    LISTA_CODURI      OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOCURI;
BEGIN
    LISTA_CODURI := OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOCURI();
    FOR DATE_LOC IN (
        SELECT
            L.*
        FROM
            (
                SELECT
                    *
                FROM
                    DIFUZARI D
                WHERE
                    D.ID_DIFUZARE = ID_DIFUZARE_PARAM
            )      D
            INNER JOIN SALI S
            ON S.ID_SALA = D.ID_SALA
            INNER JOIN LOCURI L
            ON L.ID_SALA = S.ID_SALA
    ) LOOP
        OPEN REZERVARE_LOC(DATE_LOC.ID_LOC);
        REZERVARI_PER_LOC := 0;
        LOOP
            FETCH REZERVARE_LOC INTO DATE_REZERVARE;
            EXIT WHEN REZERVARE_LOC%NOTFOUND;
            REZERVARI_PER_LOC := REZERVARI_PER_LOC + 1;
            SELECT
                (C.NUME
                 || ' '
                 || C.PRENUME) INTO NUME_COMPLET
            FROM
                CLIENTI C
            WHERE
                C.ID_CLIENT = DATE_REZERVARE.ID_CLIENT;
        END LOOP;

        CLOSE REZERVARE_LOC;
        LISTA_CODURI.EXTEND;
        IF REZERVARI_PER_LOC = 0 THEN
            LISTA_CODURI(LISTA_CODURI.COUNT) := OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOC(DATE_LOC.COD, 'Neocupat', '');
        ELSIF REZERVARI_PER_LOC = 1 THEN
            LISTA_CODURI(LISTA_CODURI.COUNT) := OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOC(DATE_LOC.COD, 'Ocupat', NUME_COMPLET);
        ELSE
            LISTA_CODURI(LISTA_CODURI.COUNT) := OCUPARE_LOCURI_LA_DIFUZARE_DATE_LOC(DATE_LOC.COD, 'Ocupat multiplu', '');
        END IF;
    END LOOP;

    RETURN LISTA_CODURI;
END OCUPARE_LOCURI_LA_DIFUZARE;